options {
    STATIC = false;
    IGNORE_CASE = false;
    UNICODE_INPUT = true;
    MULTI = true;
    VISITOR = true;
    VISITOR_EXCEPTION = "net.nokok.karaffe.parser.excptn.KaraffeParserException";
}

PARSER_BEGIN(KaraffeParser)
package net.nokok.karaffe.parser;

import net.nokok.karaffe.parser.util.*;
import java.util.*;

public class KaraffeParser{

    public KaraffeParser(String sourceCode){
        this(new java.io.StringReader(sourceCode));
    }

    public int getCurrentTokenBeginColumn(){
        return jj_input_stream.getBeginColumn();
    }

    public int getCurrentTokenEndColumn(){
        return jj_input_stream.getEndColumn();
    }

    public int getCurrentLine(){
        return jj_input_stream.getEndLine();
    }
}


PARSER_END(KaraffeParser)

TOKEN_MGR_DECLS:
{
    public static int commentNestLevel = 0;
}

//文法定義

/**

    型宣言

    type 識別子
    type 識別子
    type 識別子 : 継承元型名
    type 識別子              <- 実装インターフェース
    type 識別子 : 継承元型名 <- 実装インターフェース
    type 識別子 : 継承元型名[型パラメータ]
    type 識別子                            <- 実装インターフェース[型パラメータ]
    type 識別子 : 継承元型名[型パラメータ] <- 実装インターフェース[型パラメータ]
 */
void TypeDeclaration():
{}
{
    //type 識別子        :        継承元型名     <-             実装インターフェース
    <Type> TypeElement() (<Colon> SuperType())? (<ReverseArrow> Interfaces())? <NewLine>
}

void SuperType():
{}
{
    TypeElement()
}

void Interfaces():
{}
{
    (LOOKAHEAD(2) TypeElement())+
}

/**
    インターフェース宣言
    interface 識別子               <- 拡張インターフェース
    interface 識別子[型パラメータ] <- 拡張インターフェース[型パラメータ]
 */
void InterfaceDeclaration():
{}
{
    //interface 識別子        <-             拡張インターフェース
    <Interface> TypeElement() (<ReverseArrow> Interfaces())? <NewLine>
}

/**
    TypeAlias宣言
    修飾子 typealias 識別子 : 識別子

    ↓未実装
    修飾子 typealias 識別子 : 識別子[型パラメータ]
 */
void TypeAliasDeclaration():
{}
{
    //typealias   識別子        :       識別子
    <TypeAlias> TypeElement() <Colon> TypeElement() <NewLine>
}

/**
    変数または関数の宣言

    変数宣言
    識別子 : 型名
    識別子        = 式
    識別子 : 型名
    識別子 : 型名 = 式
    識別子 : 型名[型パラメータ] = 式
    
    関数宣言
    識別子                      = 関数リテラル
    識別子 : 型名リスト -> 型名
    識別子 : 型名リスト -> 型名 = 関数リテラル

    識別子[型パラメータ] : 型名リスト[型パラメータ] -> 型名[型パラメータ]
    識別子[型パラメータ] : 型名リスト[型パラメータ] -> 型名[型パラメータ] = 関数リテラル
*/
void VariableOrFunctionDeclaration():
{}
{
    "def" Identifier() (<Colon> ValueType())? (<EqualSign> Body())? <NewLine>
}

void ValueType():
{}
{
    TypeElement()
}

void Body():
{}
{
    Expression()
}

void Pattern():
{}
{
    Identifier() ArgVariables() (<Atmark> Guard())? <EqualSign> Expression() <NewLine>
}

void ArgVariables():
{}
{
    (LOOKAHEAD(2) Identifier())*
}

void Guard():
{}
{
    Expression()
}

/**
    Enumの宣言

    enum 識別子: [識別子 識別子...]
 */
void EnumDeclaration():
{}
{
    <Enum> Identifier() <Colon> <LeftBracket> EnumElements() <RightBracket> <NewLine>
}

void EnumElements():
{}
{
    (Identifier())+
}

/**
    型
    または
    型[型パラメータ]
 */
void TypeElement() #void:
{}
{
    TypeWithTypeParams()
  | LOOKAHEAD(2) FunctionType()
  | LOOKAHEAD(2) <LeftParen> <RightParen>
}

void TypeWithTypeParams() #void:
{}
{
    Identifier() (LOOKAHEAD(2) TypeParameters())?
}

void FunctionType():
{}
{
    <LeftParen> (TypeElement())+ SingleArrow() TypeElement() <RightParen>
}

/**
    関数リテラル

    () -> ...
    () -> { ... }
    (引数) -> ...
    (引数:型) -> ...
    (引数1 引数2...) -> ...
    (引数1:型 引数2:型 ...) -> ...
 */
void FunctionLiteral():
{}
{
    //(         引数:型                       )            ->            {            ...          }             ...
    <LeftParen> (Identifier())* <RightParen> SingleArrow() (<LeftBrace> Expression() <RightBrace> | Expression())
}

/**
    Import文

    import 識別子
    import 識別子 -> 別名

 */
void ImportStatement():
{}
{
    <Import> (LOOKAHEAD(2) ((Identifier()|JavaFQCN())) (<SingleArrow> Identifier())?)+
}

void JavaFQCN():
{}
{
    StringLiteral()   
}

/**
    extends HogeType += hoge
*/
void ExtendsType():
{}
{
    "extends" TypeElement() <AA> Identifier()
}

/**
    型パラメータ
    [型パラメータリスト]

    型パラメータリスト
    型パラメータ 型パラメータ ...

    型パラメータ
    
 */
void TypeParameters():
{}
{
    <LeftBracket> (TypeParameter())+ <RightBracket> 
}

/**
    型パラメータ

    識別子
    識別子 境界型
 */
void TypeParameter():
{}
{
    TypeElement() (TypeBound())?
}

/**
    境界型

    < 型変数
    < インターフェース & インターフェース ...
    < 型パラメータ
 */
void TypeBound():
{}
{
    <Lt> TypeElement() (<And> TypeElement())*
}

/**
    module モジュール名
 */
void ModuleDeclarationStatement():
{}
{
    <Module> Identifier()
}

/**
    アノテーション

    @識別子
 */
void Annotation():
{}
{
    <Atmark> Identifier()
}

/*
コメントの処理
*/
SKIP:
{
    " "
    | "\t"
    | < "//" (~["\r","\n"])* > 
    | "/*" {
        commentNestLevel++;
        SwitchTo(InComment);
    }
}

<InComment> SKIP:
{
    "/*" {
        commentNestLevel++;
    }
    | < ~[]>
    | <EndOfLine : <NewLine>> 
    | "*/" {
        commentNestLevel--;
        if(commentNestLevel == 0){
            SwitchTo(DEFAULT);
        }
    }
}

/**
    Reserved keywords 
*/
TOKEN:
{
    < Abstract : "abstract" >
    | < Check : "check" >
    | < Enum : "enum" >
    | < Import : "import" >
    | < Interface : "interface" >
    | < Lazy : "lazy" >
    | < Module : "module" >
    | < Operator : "op" >
    | < Override : "override" >
    | < Private : "private" >
    | < Sealed : "sealed" >
    | < Switch : "switch" >
    | < Type : "type" >
    | < UnaryOperator : "unaryop" >
    | < TypeAlias : "typealias" >
    | < Undefined : "undefined" >
    | < Variable : "var" >
}

/**
    Literals
*/
TOKEN:
{
      < BoolLiteral : "true"
                    | "false" >
    | < IntLiteral : <Zero> | <NonZero> (<Zero> | <NonZero>)* >
    | < Hexadecimal : "0" ["x","X"] (<HexDigit>)+ >
    | < #HexDigit : ["0"-"9" , "A"-"F" , "a"-"f"] > 
    | < FloatLiteral : (<Zero> | <IntLiteral>) <Dot> (<Zero> | <NonZero>)+ (<Exp> (<Minus>)? <IntLiteral> )? >
    | < #Zero : "0" >
    | < #NonZero : ["1"-"9"] >
    | < #Exp : "e" | "E" >
    | < StringLiteral : "\"" (<StringCharacter>)* "\"" >
    | < #StringCharacter : (~["\"","\\","\n","\r"]) 
                         | <StringEscapeSeq> 
                         | <UnicodeEscape> >
    | < StringEscapeSeq : "\\" ["b","t","n","f","r","\"","\\","\'"] >
    | < UnicodeEscape : "\\u" <HexDigit> <HexDigit> <HexDigit> <HexDigit> >
}

void Literal() #void:
{}
{
     FunctionLiteral()
    | BoolLiteral()
    | IntLiteral()
    | FloatLiteral()
    | StringLiteral()
    | TupleLiteral()
    | ListLiteral()
    | UndefinedLiteral()
}

/**
    Identifiers
*/
TOKEN:
{
      < Identifier : <IdentifierHead> (<IdentifierChar>)* >
    | < #IdentifierHead : [
                                    "A"-"Z", 
                                    "a"-"z", 
                                    "_",     
                                    "ぁ"-"ゖ",
                                    "ァ"-"ヺ",
                                    "\u4E00"-"\u9FFF"  //漢字     CJK Unified Ideographs
    ] >
    | < #IdentifierChar :      [
                                    "'",     
                                    "0"-"9",
                                    "A"-"Z", 
                                    "a"-"z", 
                                    "_",     
                                    "ぁ"-"ゖ",
                                    "ァ"-"ヺ",
                                    "\u4E00"-"\u9FFF"  //漢字     CJK Unified Ideographs
                               ]  >
}

Token Identifier():
{
    Token t;
}
{
    t = <Identifier> {
        jjtThis.jjtSetValue(t);
        return t;
    }
}

ASTCompileUnit CompileUnit():
{}
{
    ( LOOKAHEAD(Assignment()) Assignment() | LOOKAHEAD(MethodInvocation()) MethodInvocation() | Statement() |  LOOKAHEAD(2) EndOfBlock() | LOOKAHEAD(2) NewLineToken())* <EOF> {
        return jjtThis;
    }
}

void Statement() #void:
{}
{
     Declaration()
    | ImportStatement()
    | ModifierStatement()
    | ExtendsType()
}

void Declaration() #void:
{}
{
    LOOKAHEAD(3) VariableOrFunctionDeclaration() (LOOKAHEAD(2) Pattern())*
  | LOOKAHEAD(3) TypeDeclaration()
  | InterfaceDeclaration()
  | LOOKAHEAD(3) TypeAliasDeclaration()
  | ModuleDeclarationStatement()
  | EnumDeclaration()
}

//MethodInvocations

/**
    hoge()
    hoge.fuga().hoge()...
 */
void MethodInvocation():
{}
{
    LOOKAHEAD(2) MethodName() Arguments() (LOOKAHEAD(2) (<Dot>)? MethodName() Arguments())*
  | FunctionLiteral() Arguments()
}

void MethodName():
{}
{
    LOOKAHEAD(2) ElementAccess()
  | LOOKAHEAD(2) Identifier()
}

void ExprName():
{}
{
    Identifier()
}

void Arguments():
{}
{
    LOOKAHEAD(2) <LeftParen> <RightParen>
  | LOOKAHEAD(2) <LeftParen> Argument() ("," Argument())* <RightParen>
}

void Argument():
{}
{
    Expression()
}

/**
    ModuleName::ElementName...
 */
void ExplicitModuleElementAccess():
{}
{
    Identifier() <DoubleColon> Identifier()
}

void Expression() #void:
{}
{
    LOOKAHEAD(3) OrExpr()
  | LOOKAHEAD(3) Assignment()
}

void Assignment():
{}
{
    LeftHandSide() AssignmentOperator() Expression()
}

void LeftHandSide():
{}
{
    LOOKAHEAD(2) ElementAccess()
  | LOOKAHEAD(2) Identifier()
}

void AssignmentOperator() #void:
{}
{
    Assign() | AA() | SA() | MA() | DA() | RA()
}

void OrExpr()#void:
{}
{
    (AndExpr() (LOOKAHEAD(2) Or() AndExpr())*) #ExprNode(>1)
}

void AndExpr()#void:
{}
{
    (Equality() (LOOKAHEAD(2) And() Equality())*) #ExprNode(>1)
}

void Equality()#void:
{}
{
    (Relational() (LOOKAHEAD(2) (EqualTo()|
                                NotEqualTo()) Relational())*) #ExprNode(>1)
}

void Relational()#void:
{}
{
    (Range() (LOOKAHEAD(2) (LessThan() |
                 GreaterThan()|
                 LessThanEqualTo() |
                 GreaterThanEqualTo()) Range())*) #ExprNode(>1)
}

void Range()#void:
{}
{
    (Additive() (LOOKAHEAD(2) ( ClosedRange() | 
                              HalfOpenRange()) Additive())*) #ExprNode(>1)
}

void Additive() #void:
{}
{
    (Multiplicative() (LOOKAHEAD(2) (Plus() | 
                                    Minus()) Multiplicative())*) #ExprNode(>1)
}

void Multiplicative() #void:
{}
{
    (UnaryExpr() (LOOKAHEAD(2) (Star()|
                               Slash()|
                               SafeDiv()|
                               SafeRem()|
                               Percent()|
                               Hat()) UnaryExpr())* ) #ExprNode(>1)
}

void UnaryExpr() #void:
{}
{
   UnaryPlus()  UnaryExprNotPlusMinus() 
  | UnaryMinus() UnaryExprNotPlusMinus()
  | UnaryExprNotPlusMinus()
}

void UnaryExprNotPlusMinus() #void:
{}
{
    (UnaryBang())* Primary()
}

void Primary() #void:
{}
{
    ( LOOKAHEAD(3) Literal()
    | LOOKAHEAD(3) MethodInvocation()
    | LOOKAHEAD(2) Identifier()
    | LOOKAHEAD(3) <LeftParen> Expression() <RightParen> ) #ExprNode(>1)
}

/**
    Identifier.Identifier...
 */
void ElementAccess():
{}
{
    Identifier() (<Dot> Identifier())+
}

void ModifierStatement():
{}
{
     "#" (AllModifier())+
}

void AllModifier() #void:
{}
{
    AbstractModifier() 
  | OverrideModifier() 
  | PrivateModifier() 
  | SealedModifier() 
  | LazyModifier() 
  | OpModifier()
  | UnaryOpModifier()
  | VariableModifier()
}

/**
    Delimiters
*/
TOKEN:
{
      < LeftBracket : "[" >
    | < RightBracket : "]" >
    | < LeftBrace : "{" >
    | < RightBrace : "}" >
    | < LeftParen : "(" >
    | < RightParen : ")" >
    | < Colon : ":" >
    | < DoubleColon : "::">
    | < Dot : "." >
}

/**
    Operators
 */
TOKEN:
{

    < EqualSign : "=" >
    | < AA : "+=" >
    | < SA : "-=" >
    | < MA : "*=" >
    | < DA : "/=" >
    | < RA : "%=" >
    | < Lt : "<" >
    | < LtE : "<=" >
    | < Gt : ">" >
    | < GtE : ">=" >
    | < Plus : "+" >
    | < Minus : "-" >
    | < Star : "*" >
    | < Slash : "/" >
    | < SafeDiv : "/?" >
    | < Percent : "%" >
    | < SafeRem : "%?" >
    | < Tilde : "~" >
    | < Bang : "!" >
    | < And : "&" > 
    | < Or : "|" >
    | < Hat : "^" >
    | < Question : "?" >
    | < ReverseArrow : "<-" >
    | < ReverseArrowChar : "←" >
    | < SingleArrowChar : "→" >
    | < SingleArrow : "->" >
    | < CastArrow : "~>" >
    | < TypeCheckArrow : "~>?" >
    | < DoubleArrow : "=>" >
    | < EqualTo : "==" >
    | < NotEqualTo : "!=" >
    | < NonComparable : "!<>=" >
    | < Comparable : "<>=" >
    | < ClosedRange : ".." >
    | < HalfOpenRange : ".<" >

}

/**
    Other tokens
*/
TOKEN:
{
      < NewLine : "\r"
                | "\n" 
                | "\r\n" >
      | < Atmark : "@" >
}


void AbstractModifier():
{
    Token t;
}
{
    t = <Abstract> {
        jjtThis.jjtSetValue(t);
    }
}

void OverrideModifier():
{
    Token t;    
}
{
    t = <Override> {
        jjtThis.jjtSetValue(t);
    }
}

void PrivateModifier():
{
    Token t;
}
{
    t = <Private> {
        jjtThis.jjtSetValue(t);
    }
}

void SealedModifier():
{
    Token t;
}
{
    t = <Sealed> {
        jjtThis.jjtSetValue(t);
    }
}

void LazyModifier():
{
    Token t;
}
{
    t = <Lazy> {
        jjtThis.jjtSetValue(t);
    }
}

void OpModifier():
{
    Token t;
}
{
    t = <Operator> {
        jjtThis.jjtSetValue(t);
    }
}

void UnaryOpModifier():
{
    Token t;
}
{
    t = <UnaryOperator> {
        jjtThis.jjtSetValue(t);
    }
}

void VariableModifier():
{
    Token t;
}
{
    t = <Variable> {
        jjtThis.jjtSetValue(t);
    }
}

void UnaryPlus():
{
    Token t;
}
{
    t = <Plus> {
        jjtThis.jjtSetValue(t);
    }
}

void UnaryBang():
{
    Token t;
}
{
    t = <Bang> {
        jjtThis.jjtSetValue(t);
    }
}

void UnaryMinus():
{
    Token t;
}
{
    t = <Minus> {
        jjtThis.jjtSetValue(t);
    }
}

void EndOfBlock():
{}
{
    <NewLine> <NewLine>
}

void NewLineToken():
{}
{
    <NewLine>
}

void Star():
{
    Token t;
}
{
    t = <Star> {
        jjtThis.jjtSetValue(t);
    }
}

void Slash():
{
    Token t;
}
{
    t = <Slash> {
        jjtThis.jjtSetValue(t);
    }
}

void Percent():
{
    Token t;
}
{
    t = <Percent> {
        jjtThis.jjtSetValue(t);
    }
}

void SafeDiv():
{
    Token t;
}
{
    t = <SafeDiv> {
        jjtThis.jjtSetValue(t);
    }
}

void SafeRem():
{
    Token t;
}
{
    t = <SafeRem> {
        jjtThis.jjtSetValue(t);
    }
}

void Hat():
{
    Token t;
}
{
    t = <Hat> {
        jjtThis.jjtSetValue(t);
    }
}

void Plus():
{
    Token t;
}
{
    t = <Plus> {
        jjtThis.jjtSetValue(t);
    }
}

void Minus():
{
    Token t;
}
{
    t = <Minus> {
        jjtThis.jjtSetValue(t);
    }
}

void EqualTo():
{
    Token t;
}
{
    t = <EqualTo> {
        jjtThis.jjtSetValue(t);
    }
}

void NotEqualTo():
{
    Token t;
}
{
    t = <NotEqualTo> {
        jjtThis.jjtSetValue(t);
    }
}

void GreaterThan():
{
    Token t;
}
{
    t = <Gt> {
        jjtThis.jjtSetValue(t);
    }
}

void LessThan():
{
    Token t;
}
{
    t = <Lt> {
        jjtThis.jjtSetValue(t);
    }
}

void GreaterThanEqualTo():
{
    Token t;
}
{
    t = <GtE> {
        jjtThis.jjtSetValue(t);
    }
}

void LessThanEqualTo():
{
    Token t;
}
{
    t = <LtE> {
        jjtThis.jjtSetValue(t);
    }
}

void Comparable():
{
    Token t;
}
{
    t = <Comparable> {
        jjtThis.jjtSetValue(t);
    }
}

void NonComparable():
{
    Token t;
}
{
    t = <NonComparable> {
        jjtThis.jjtSetValue(t);
    }
}

void And():
{
    Token t;
}
{
    t = <And> {
        jjtThis.jjtSetValue(t);
    }
}
void Or():
{
    Token t;
}
{
    t = <Or> {
        jjtThis.jjtSetValue(t);
    }
}


void BoolLiteral():
{
    Token t;
}
{
    t = <BoolLiteral> {
        jjtThis.jjtSetValue(t);
    }
}


void IntLiteral():
{
    Token t;
}
{
    t = <IntLiteral> {
        jjtThis.jjtSetValue(t);
    }
    | t = <Hexadecimal> {
        jjtThis.jjtSetValue(t);
    }
}

void FloatLiteral():
{
    Token t;
}
{
    t = <FloatLiteral> {
        jjtThis.jjtSetValue(t);
    }
}

void StringLiteral():
{
    Token t;
}
{
    t = <StringLiteral> {
        jjtThis.jjtSetValue(t);
    }
}

void ListLiteral():
{}
{
    <LeftBracket> (Literal())* <RightBracket>
}

void TupleLiteral():
{
    Token t;
}
{
    t = "#(" (LOOKAHEAD(2) Expression())* <RightParen>
}

void UndefinedLiteral():
{
    Token t;
}
{
    t = <Undefined> {
        jjtThis.jjtSetValue(t);
    }
}

void SingleArrow():
{
    Token t;
}
{
    t = <SingleArrow> | t = <SingleArrowChar> {
        jjtThis.jjtSetValue(t);
    }
}
void Assign():
{
    Token t;
}
{
    t = <EqualSign> {
        jjtThis.jjtSetValue(t);
    }
}

void ClosedRange():
{
    Token t;
}
{
    t = <ClosedRange> {
        jjtThis.jjtSetValue(t);
    }
}

void HalfOpenRange():
{
    Token t;
}
{
    t = <HalfOpenRange> {
        jjtThis.jjtSetValue(t);
    }
}

void AA():
{
    Token t;
}
{
    t = <AA> {
        jjtThis.jjtSetValue(t);
    }
}

void SA():
{
    Token t;
}
{
    t = <SA> {
        jjtThis.jjtSetValue(t);
    }
}

void MA():
{
    Token t;
}
{
    t = <MA> {
        jjtThis.jjtSetValue(t);
    }
}

void DA():
{
    Token t;
}
{
    t = <DA> {
        jjtThis.jjtSetValue(t);
    }
}

void RA():
{
    Token t;
}
{
    t = <RA> {
        jjtThis.jjtSetValue(t);
    }
}

