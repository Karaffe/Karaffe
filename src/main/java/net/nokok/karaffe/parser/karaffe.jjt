//Karaffe Javacc file

options {
    STATIC = false;
    IGNORE_CASE = false;
    UNICODE_INPUT = true;
    MULTI = true;
    VISITOR = true;
    VISITOR_EXCEPTION = "net.nokok.karaffe.parser.excptn.KaraffeParserException";
}

PARSER_BEGIN(KaraffeParser)
package net.nokok.karaffe.parser;

public class KaraffeParser{

    public KaraffeParser(String sourceCode){
        this(new java.io.StringReader(sourceCode));
    }

    public int getCurrentTokenBeginColumn(){
        return jj_input_stream.getBeginColumn();
    }

    public int getCurrentTokenEndColumn(){
        return jj_input_stream.getEndColumn();
    }

    public int getCurrentLine(){
        return jj_input_stream.getEndLine();
    }
}


PARSER_END(KaraffeParser)

TOKEN_MGR_DECLS:
{
    public static int commentNestLevel = 0;
}

/*
コメントの処理
*/
SKIP:
{
    " "
    | "\t"
    | < "//" (~["\n"])* <NewLine> >
    | "/*" {
        commentNestLevel++;
        SwitchTo(InComment);
    }
}

<InComment> SKIP:
{
    "/*" {
        commentNestLevel++;
    }
    | <EndOfLine : <NewLine>> 
    | "*/" {
        commentNestLevel--;
        if(commentNestLevel == 0){
            SwitchTo(DEFAULT);
        }
    }
}

/**
    Reserved keywords 
*/
TOKEN:
{
      < Check : "check" >
    | < Interface : "interface" >
    | < Public : "public" >
    | < SideEffect : "seffect" >
    | < Switch : "switch" >
    | < To : "to" >
    | < Type : "type" >
    | < Undefined : "undefined" >
}

/**
    Literals
*/
TOKEN:
{
      < BoolLiteral : "true"
                    | "false" >
    | < IntLiteral : <Zero> | (<Minus>)? <NonZero> (<Zero> | <NonZero>)* >
    | < Hexadecimal : (<Minus>)? "0" ["x","X"] (<HexDigit>)+ >
    | < #HexDigit : ["0"-"9" , "A"-"F" , "a"-"f"] > 
    | < FloatLiteral : (<Minus>)? (<Zero> | <IntLiteral>) <Dot> (<Zero> | <NonZero>)+ (<Exp> <IntLiteral> )? >
    | < #Zero : "0" >
    | < #NonZero : ["1"-"9"] >
    | < #Exp : "e" | "E" >
    | < StringLiteral : "\"" (<StringCharacter>)* "\"" >
    | < #StringCharacter : (~["\"","\\","\n","\r"]) 
                         | <StringEscapeSeq> 
                         | <UnicodeEscape> >
    | < StringEscapeSeq : "\\" ["b","t","n","f","r","\"","\\","\'"] >
    | < UnicodeEscape : "\\u" <HexDigit> <HexDigit> <HexDigit> <HexDigit> >
}

/**
    Identifiers
*/
TOKEN:
{
      < Identifier : <IdentifierHead> (<IdentifierHead> | <IdentifierCharacter>)* ("[" "]")?>
    | < #IdentifierHead :      [
                                    "\u0041"-"\u005A", //A-Z
                                    "\u0061"-"\u007A", //a-z
                                    "\u3040"-"\u309F", //ひらがな Hiragana
                                    "\u30A0"-"\u30FF", //カタカナ Katakana
                                    "\u4E00"-"\u9FFF"  //漢字     CJK Unified Ideographs
                               ]  >
    | < #IdentifierCharacter : [
                                    "\u0030"-"\u0039", //0-9
                                    "\u0041"-"\u005A", //A-Z
                                    "\u0061"-"\u007A", //a-z
                                    "\u005F",          //_
                                    "\u3040"-"\u309F", //ひらがな Hiragana
                                    "\u30A0"-"\u30FF", //カタカナ Katakana
                                    "\u4E00"-"\u9FFF"  //漢字     CJK Unified Ideographs
                               ] >
    | < TypeParameter : ["\u0041"-"\u005A"] > //A-Z
}

/**
    Separators
*/
TOKEN:
{
      < LeftBracket : "[" >
    | < RightBracket : "]" >
    | < LeftBrace : "{" >
    | < RightBrace : "}" >
    | < Colon : ":" >
    | < Dot : "." >
}

/**
    Operators
 */
TOKEN:
{

    < EqualSign : "=" >
    | < Lt : "<" >
    | < Gt : ">" >
    | < Plus : "+" >
    | < Minus : "-" >
    | < Star : "*" >
    | < Slash : "/" >
    | < Rem : "%" >
    | < Tilde : "~" >
    | < Bang : "!" >
    | < And : "&" > 
    | < Or : "|" >
    | < Hat : "^" >
    | < Hook : "?" >
    | < Arrow : "=>" >

}

/**
    Other tokens
*/
TOKEN:
{
      < NewLine : "\r"
                | "\n" 
                | "\r\n" >
}

/**
 * CompileUnit = (Statement)* EOF
 */
public ASTCompileUnit CompileUnit():
{
}
{
    (Statement())* <EOF> {
        return jjtThis;
    }
}

//Statement

/**
    Statement = Declaration
 */
void Statement():
{}
{
    Declaration()
}


// Declaration

/**
 Declaration = TypeDcl
             | InterfaceDcl
 */
void Declaration():
{}
{
     LOOKAHEAD(3) TypeDcl()
     | InterfaceDcl()
     | VariableDcl()
}

/**
    TypeDcl         = SimpleTypeDcl 
                    | TypeAlias
                    | Inheritance
    SimpleTypeDcl   = "type" Identifier TypeParameters?
    TypeAlias       = SimpleTypeDcl "=" (Identifier TypeParameters?)+
    Inheritance     = SimpleTypeDcl "<" (Identifier TypeParameters?)+
    
    e.g.
    type Foo 

    type Bar[A]

    type Baz[A B C]

    //TypeAlias
    type Foo = Bar

    type Foo[A] = Bar[A]

    //Inheritance
    type Foo < Bar

    type Foo[A] < Bar[A]

 */
void TypeDcl():
{}
{
    <Type> Identifier() (TypeParameters())? (Declaration())* ((EqualOperator() | LessThanOperator()) (Identifier() (TypeParameters())?)*)* NewLine()
}

/**
    InterfaceDcl = "interface" InterfaceName TypeParameters? NewLine NestDeclaration

    e.g.
    interface Foo

    interface Foo[A]
 */
void InterfaceDcl():
{}
{
    <Interface> Identifier() NewLine()
}

/**
    VariableDcl     = Identifier VariableTypeDcl? "=" Expression NewLine
    VariableTypeDcl = VariableType
                    | FunctionType
 */
void VariableDcl():
{}
{
    Identifier() (<Colon> VariableTypeDcl())? EqualOperator() Expression() NewLine()
}

/**
    VariableTypeDcl = (Identifier TypeParameters?)+ ("to" Identifier TypeParameters?)? 
 */
void VariableTypeDcl():
{}
{
    (Identifier() (TypeParameters())?)+ (<To> Identifier() TypeParameters())? 
}

/**
    TypeParameters = "[" TypeParameter1 TypeParameter2 ... "]"
 */
void TypeParameters():
{}
{
    "[" (Identifier())+ "]"
}

//Expressions

/**
    Expression = Literal
 */
void Expression():
{}
{
    Literal()
}


/**
    Literal = BoolLiteral
            | IntLiteral
            | FloatLiteral
            | StringLiteral
            | UndefinedLiteral
 */
void Literal():
{
    Token t;
}
{
      t = <BoolLiteral> {
        jjtThis.jjtSetValue(t);
    }
    | t = <IntLiteral> {
        jjtThis.jjtSetValue(t);
    }
    | t = <FloatLiteral> {
        jjtThis.jjtSetValue(t);
    }
    | t = <StringLiteral> {
        jjtThis.jjtSetValue(t);
    } t = <Undefined> {
        jjtThis.jjtSetValue(t);
    }
    | FunctionLiteral()
}

/**
    FunctionLiteral = "[" FunctionVariableBinding "]" "=>" "{"? Expression "}"?

    e.g.

    [] => {...}
 */
void FunctionLiteral():
{}
{
    <LeftBracket> (FunctionVariableBinding())* <RightBracket> ArrowOperator() <LeftBrace> Expression() <RightBrace>
}

/**
    FunctionVariableBinding = Identifier
                            | DefaultValueBinding
                            | ExplicitTypeBinding
                            | ExplicitTypeDefaultBinding
    DefaultValueBinding     = Identifier "=" Literal
    ExplicitTypeBinding     = Identifier ":" Identifier
    ExplicitTypeDefaultBinding = ExplicitTypeBinding "=" Literal

 */
void FunctionVariableBinding():
{}
{
    Identifier() (<Colon> Identifier() EqualOperator() Literal())?
}

//Identifiers

/**
    Identifier = IdentifierHead IdentifierCharacter
 */
void Identifier():
{
    Token t;
}
{
    t = <Identifier> {
        jjtThis.jjtSetValue(t);
    }
}

void EqualOperator():
{}
{
    <EqualSign>
}

void LessThanOperator():
{}
{
    <Lt>
}

void GreaterThanOperator():
{}
{
    <Gt>
}

void Plus():
{}
{
    <Plus>
}

void Minus():
{}
{
    <Minus>
}
void ArrowOperator():
{}
{
    <Arrow>
}

void NewLine():
{}
{
    <NewLine>
}

