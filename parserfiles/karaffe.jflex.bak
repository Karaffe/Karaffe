/*
    Karaffe Programming Langage Lexer Specification
*/

package karaffe.compiler;
import java_cup.runtime.*;

%%
%public
%class Lexer
%implements sym

%unicode

%cup

%line
%column

%{

    private final java.lang.StringBuilder stringBuilder = new java.lang.StringBuilder();

    private Symbol sym(int type) {
        return new Symbol(type, yyline + 1, yycolumn + 1);
    }
    
    private Symbol sym(int type, Object value) {
        return new Symbol(type, yyline + 1, yycolumn + 1, value);
    }

    public int line() {
        return yyline;
    }

    public int column() {
        return yycolumn;
    }

%}

LineTerminator = \r
               | \n
               | \r\n

InputCharacter = [^\r\n]

WhiteSpace = {LineTerminator}
           | [ \t\f]

Comment = {TraditionalComment}
        | {SingleLineComment}
        | {DocumentationComment}

TraditionalComment = "/*" [^*] ~"*/"
                   | "/*" "*"+ "/"
SingleLineComment = "//" {InputCharacter}* {LineTerminator}?
DocumentationComment = "/**" "*"* [^/*] ~"*/"

IntLiteral = [1-9][0-9]*

Identifier = [:jletter:][:jletterdigit:]*

%state STRING

%%
<YYINITIAL> {

    "package"                   {return sym(PACKAGE);}
    "import"                    {return sym(IMPORT);}
    "class"                     {return sym(CLASS);}
    "def"                       {return sym(DEF);}
    "="                         {return sym(EQ);}
    "{"                         {return sym(LBRACE);}
    "}"                         {return sym(RBRACE);}
    "("                         {return sym(LPAREN);}
    ")"                         {return sym(RPAREN);}
    ","                         {return sym(COMMA);}
    "."                         {return sym(DOT);}
    "+"                         {return sym(PLUS);}
    "-"                         {return sym(MINUS);}
    "*"                         {return sym(STAR);}
    "/"                         {return sym(SLASH);}
    ";"                         {return sym(SEMICOLON);}

    {Identifier}                {return sym(IDENTIFIER, yytext());}
    {WhiteSpace}                {}
    {Comment}                   {return sym(COMMENT, yytext());}
    {IntLiteral}                {return sym(INT_LITERAL, yytext());}
    \"                          {stringBuilder.setLength(0); yybegin(STRING);}
}

<STRING> {
    \"                          {yybegin(YYINITIAL);return sym(STRING_LITERAL, stringBuilder.toString());}
    [^\n\r\"\\]+                {stringBuilder.append(yytext());}
    \\t                         {stringBuilder.append('\t');}
    \\n                         {stringBuilder.append('\n');}
    \\r                         {stringBuilder.append('\r');}
    \\\"                        {stringBuilder.append('\"');}
    \\                          {stringBuilder.append('\\');}
}

[^]                             {throw new Error("Illegal Character" + yytext());}

<<EOF>> {
    return sym(EOF);
}
