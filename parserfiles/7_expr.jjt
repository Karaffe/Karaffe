//7_expr.jjt
//**********************************************************************
//Expressions
//**********************************************************************
void Expression():{}
{
    LOOKAHEAD(AssignmentExpr()) AssignmentExpr() |
    LOOKAHEAD(PipelineExpr()) PipelineExpr() |
    IfExpr() |
    SwitchExpr() |
    WhileLoopExpr()
}

void AssignmentExpr()#void:{}
{
    (LeftHandSide() AssignmentOp() Expression())#AssignmentExpr(>1)
}

void AssignmentOp():{}
{
    ("="|"+="|"-="|"*="|"/="|"%="|"&="|"|="|"^=") 
}

void LeftHandSide():{}
{
    ExpressionName()
}

void PipelineExpr()#void:{}
{
    BoolOrExpr() (("|>" #BackwardPipelineExpr() | "<|" #PipelineExpr()) BoolOrExpr() #ExprNode(2))*
}

void BoolOrExpr()#void:{}
{
    BoolAndExpr() ("|" #BoolOr() BoolAndExpr() #ExprNode(2))*
}

void BoolAndExpr()#void:{}
{
    EqualityExpr() ("&" #BoolAnd() EqualityExpr() #ExprNode(2))*
}

void EqualityExpr()#void:{}
{
    RelationalExpr() (("==" #EqualTo()| "!="#NotEqualTo()) RelationalExpr() #ExprNode(2))*
}

void RelationalExpr()#void:{}
{
    RangeExpr() (("<" #LessThan()| ">" #GreaterThan()| "<=" #LessThanEqTo()| ">=" #GreaterThanEqTo()) RangeExpr() #ExprNode(2))*
}

void RangeExpr()#void:{}
{
    AdditiveExpr() ((".." #ClosedRange()|".<" #HalfOpenRange()) AdditiveExpr() #ExprNode(2))*
}

void AdditiveExpr()#void:{}
{
    MultiplicativeExpr() (LOOKAHEAD(("+"|"-"|"+?"|"-?") MultiplicativeExpr()) ("+" #Add()|"-" #Sub()|"+?" #SafeAdd()|"-?" #SafeSub()) MultiplicativeExpr() #ExprNode(2))*
}

void MultiplicativeExpr()#void:{}
{
    UnaryExpr() (("*" #Mul()|"/" #Div()|"%" #Rem()|"*?" #SafeMul()|"/?"#SafeDiv()|"%?"#SafeRem()) UnaryExpr() #ExprNode(2))*
}

void UnaryExpr()#void:{}
{
   ("+" #UAdd() UnaryExpr() |
    "-" #USub() UnaryExpr() |
    UnaryExprNotPlusMinus() )#ExprNode(>1)
}

void UnaryExprNotPlusMinus()#void:{}
{
   ("!" #UBang() UnaryExpr() |
    LOOKAHEAD(CastExpr()) CastExpr() |
    LOOKAHEAD(CheckExpr()) CheckExpr() |
    PostFixExpr() ) #ExprNode(>1)
}

void CastExpr()#void:{}
{
    ("(" Expression() ")" "~>" ClassType()) #ExprNode(>1)
}

void CheckExpr()#void:{}
{
    ("(" Expression() ")" "~>?" ClassType()) #ExprNode(>1)
}

void PostFixExpr()#void:{}
{
   (LOOKAHEAD(Primary()) Primary() |
    LOOKAHEAD(ExpressionName()) ExpressionName()) #ExprNode(>1)
}

void Primary()#void:{}
{
    LOOKAHEAD(AmbiguousName() "::") MethodReference() |
    LOOKAHEAD(MethodInvocation()) MethodInvocation() |
    LOOKAHEAD(Literal()) Literal() |
    LOOKAHEAD(2) This() |
    Self() |
    LOOKAHEAD("(" Expression() ")") "(" Expression() ")" |
    LOOKAHEAD(ListDisplay()) ListDisplay() |
    LOOKAHEAD(DictionaryDisplay()) DictionaryDisplay() |
    LOOKAHEAD(TupleDisplay()) TupleDisplay() |
    LOOKAHEAD(AmbiguousName()) AmbiguousName()

}

void MethodReference():{}
{
    AmbiguousName() "::" MethodName()
}

void ListDisplay():{}
{
    "[" (Expression() (Expression())*)? "]"
}

void DictionaryDisplay():{}
{
    "[" (DictionaryPair())+ "]"
}

void DictionaryPair():{}
{
    Identifier() "->" Expression() 
}

void TupleDisplay():{}
{
    "(" (Expression() (Expression())*)? ")"
}

void IfExpr():{}
{
    "if" Expression() #IfBodyStart() IfBody() #IfBodyEnd() (LOOKAHEAD("else" IfBody()) "else" #ElIfBodyStart IfBody() #ElIfBodyEnd())?
}

void IfBody():{}
{
    BlockStmt() |
    "{" BlockStmts() "}"
}

void SwitchExpr():{}
{
    "match" "(" Expression() ")" "{" (SwitchCase())+ "}"
}

void SwitchCase():{}
{
    "case" SwitchCond() "=>" CaseBody()
}

void SwitchCond():{}
{
    (LOOKAHEAD(IdentifierTypeBinding()) IdentifierTypeBinding() |
     LOOKAHEAD(Expression()) Expression()) (SwitchGuard())?
}

void SwitchGuard():{}
{
    "@" Expression()
}

void CaseBody():{}
{
    BlockStmt() |
    "{" BlockStmts() "}"
}

void WhileLoopExpr():{}
{
    "while" "(" Expression() ")" WhileBody()
}

void WhileBody():{}
{
    BlockStmt() |
    "{" BlockStmts() "}"
}

//**********************************************************************
//MethodInvocation
//**********************************************************************
void MethodInvocation():{}
{ 
    LOOKAHEAD(AmbiguousName() "{") MethodInvocationWithBlockArguments() |
    LOOKAHEAD(2) MethodName() ParenArguments() (DotMethodNameArguments())* | 
    LOOKAHEAD(2) ExpressionName() ParenArguments() (DotMethodNameArguments())*|
    //Primary MethodInvocation
    LOOKAHEAD(FunctionLiteral() ParenArguments()) FunctionLiteral() ParenArguments() (DotMethodNameArguments())* |
    LOOKAHEAD(2) Literal() (DotMethodNameArguments())+ |
    This() (DotMethodNameArguments())+ |
    Self() (DotMethodNameArguments())+ |
    LOOKAHEAD("(" Expression() ")"  (DotMethodNameArguments())+) "(" Expression() ")"  (DotMethodNameArguments())+ |
    LOOKAHEAD(ListDisplay()) ListDisplay() (DotMethodNameArguments())+ |
    LOOKAHEAD(DictionaryDisplay()) DictionaryDisplay() (DotMethodNameArguments())+ |
    TupleDisplay() (DotMethodNameArguments())+  |
    LOOKAHEAD(2) AmbiguousName() (DotMethodNameArguments())+ 
}

void ParenArguments()#void:{}
{
    "(" Arguments() ")"
}

void DotMethodNameArguments() #void:{}
{
    LOOKAHEAD("." AmbiguousName() "{") "." MethodInvocationWithBlockArguments()|
    LOOKAHEAD("." MethodName() "(")"." MethodName() ParenArguments()|
    LOOKAHEAD("." Identifier())"." Identifier()
}

void MethodName():{}
{
    Identifier()
}

void ExpressionName():{}
{
    AmbiguousName()
}

void Arguments():{}
{
    (Argument() ((",")? Argument())*)?
}

void Argument():{}
{
    Expression()
}

void FunctionLiteral():{}
{
    FuncLiteralArg() "->" FuncLiteralBody()
}

void FuncLiteralArg():{}
{
    LOOKAHEAD("(" (Identifier())* ")") "(" (Identifier())* ")"  |
    LOOKAHEAD("(" IdentifierTypeBindings() ")") "(" IdentifierTypeBindings() ")" |
    LOOKAHEAD(2) IdentifierTypeBinding() |
    LOOKAHEAD(2) Identifier() 
}

void FuncLiteralBody():{}
{
    "{" BlockStmts() "}"
}

void IdentifierTypeBindings():{}
{
    (IdentifierTypeBinding() ("," IdentifierTypeBinding())*)?
}

void IdentifierTypeBinding():{}
{
    Identifier() Type()
}

void AmbiguousName():{}
{
    Identifier() (LOOKAHEAD("." Identifier()) "." Identifier())*
}

void MethodInvocationWithBlockArguments()#void:{}
{
    AmbiguousName() "{" BlockStmts() "}" (LOOKAHEAD((AmbiguousName())? "{") (AmbiguousName())? "{" BlockStmts() "}")*
}

