//4_compileunit.jjt
ASTCompileUnit CompileUnit():{}
{
    (PackageDecl())? (ImportStmt() | FuncAlias() | TypeDecl())* <EOF> {
        return jjtThis;
    }
}

void PackageDecl():{}
{
    "package" ModuleName()
}

void ModuleName()#void:{}
{
    AmbiguousName()
}

void ImportStmt():{}
{
    LOOKAHEAD("import" TypeName() "->") AliasImport() |
    LOOKAHEAD("import" TypeName()) SimpleImport() |
    URLImport()
}

void SimpleImport():{}
{
    "import" (LOOKAHEAD(TypeName()) TypeName() ("." "*" #StarImport())?)+
}

void AliasImport():{}
{
    "import" (LOOKAHEAD(TypeName() "->") TypeName() ImportAfter())+
}

void ImportAfter():{}
{
    "->" Identifier()
}

void URLImport():{}
{
    "import" (LOOKAHEAD(StringLiteral()) StringLiteral())+
}

void TypeName():{}
{
    AmbiguousName()
}

//**********************************************************************
//Statement
//**********************************************************************

void Statement():{}
{
    FuncAlias() |
    LOOKAHEAD(PrivateImportStmt()) PrivateImportStmt() |
    LOOKAHEAD(ImportStmt()) ImportStmt()
}

void FuncAlias():{}
{
    "funcalias" FunctionName() ParenFormalParams() "->" Identifier() ParenFormalParams()
}

void FunctionName():{}
{
    AmbiguousName()
}

void PrivateImportStmt():{}
{
    "import" (LOOKAHEAD(TypeName() (ImportAfter())?) TypeName() (ImportAfter())?)+ PrivateImportBlock()
}

void PrivateImportBlock():{}
{
    "{" BlockStmts() "}"
}

void Annotations():{}
{
    (Annotation())*   
}

void Annotation():{}
{
    "@" Identifier() (ParenArguments())?
}
